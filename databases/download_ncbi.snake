##########################
#
# download_ncbi.snake
#
# Retrieves latest nr or nt database from NCBI and formats for lastal8
#
###########################
import ftplib, re, os, datetime, getpass
from snakemake.remote.FTP import RemoteProvider as FTPRemoteProvider

FTP = FTPRemoteProvider()

def get_file_date(host, path,
                  password=None,
                  username='ftp',
                  fmt_string="%Y%m%d"):
    """ Return modify time of file on FTP server """
    if password is None:
        raise Exception("Please supply a password for the FTP server!")

    connection = ftplib.FTP(host=host,
                            user=username,
                            passwd=password)
    mdtm_string = connection.sendcmd("MDTM " + path)
    return datetime.datetime.strptime(mdtm_string.split()[-1], "%Y%m%d%H%M%S") \
                            .strftime(fmt_string)


## Configurable params
#
# where to put the downloaded files:
seqdb_root = config.get('seqdb_root','seqdbs')
#
# where to get the files from
hostname = config.get('hostname', 'ftp.ncbi.nlm.nih.gov')
username = config.get('username', 'ftp')
password = config.get('email', '{}@hawaii.edu'.format(getpass.getuser()))

# fasta to download
db_name = config.get('database', 'nt')
db_group = config.get('db_group', 'NCBI')
remote_db_path = config.get('remote_db_path', '/blast/db/FASTA/{db}.gz'.format(db=db_name))
db_url = hostname + remote_db_path

# use modification time as a version
timestamp = config.get(
                'timestamp',
                get_file_date(hostname, remote_db_path, password, username)
            )


# Where to put the downloaded and formatted files
db_dir = os.path.join(seqdb_root, db_group)
db_base = os.path.join(db_dir, db_name, db_name + "_" + timestamp)
db_fasta = db_base + ".fasta"

# other options
fmt_threads = config.get('fmt_threads', 20)

alphabet = config.get('alphabet',
                      'prot' if re.search(r'(nr|prot)', db_name, re.I) else 'nucl')
is_prot = re.search(r'^nucl', alphabet, re.I) is None
    
# configure output files
outputs = set()
outputs.add(db_fasta + ".stats")   # fasta stats by prinseq
formatter_config = {
    'last8': {'base':'.ldb8/lastdb', 'suff': 'prj'},
    'last': {'base':'.ldb/lastdb', 'suff': 'prj'},
    'diamond': {'base':'.dmd/diamond', 'suff': 'dmnd'},
    'blast': {'base':'.bdb/blastdb', 'suff': 'pal'},
    'bwa': {'base': '.bwa/bwadb', 'suff': 'bwt', 'nucl_only':True}
}

# how should the db be formatted?
db_fmt_list = config.get('formatters', ['last8',])
if isinstance(db_fmt_list, str):
    # allow for semicolon separated list
    db_fmt_list = db_fmt_list.split(';')

for db_formatter in db_fmt_list:
    fcnf = formatter_config[db_formatter]
    if is_prot and fcnf.get('nucl_only', False):
        raise Exception("Cant format protein DB for " + db_formatter)
    db_loc = db_base + fcnf['base']
    db_fmt_file = db_loc + "." + fcnf['suff']
    outputs.add(db_fmt_file)

# taxonomy
# ftp://ftp.ncbi.nlm.nih.gov/pub/taxonomy/taxdump.tar.gz
# ftp://ftp.ncbi.nlm.nih.gov/pub/taxonomy/accession2taxid/prot.accession2taxid.gz
# for nucl: nucl_X.accession2taxid.gz for X in est, gb, gss, wgs
tax_prefixes = ['prot'] if is_prot \
               else ['nucl_' + x for x in ['est', 'gb', 'gss', 'wgs']]
remote_tax_prefix = '/pub/taxonomy'
local_tax = 'taxdump'
tax_map_files = ['{}/accession2taxid/{}.accession2taxid'.format(local_tax, p) for p in tax_prefixes]
remote_taxdump = hostname + remote_tax_prefix + '/taxdump.tar.gz'
local_tax_files = os.path.join(db_group, 'taxdump')

rule all:
    input: outputs

rule lastdb:
    input: '{db_root}.fasta'
    output: '{db_root}' + formatter_config['last']['base'] \
                  + "." + formatter_config['last']['suff']
    params:
        prot="-p" if is_prot else "",
        db='{db_root}' + formatter_config['last']['base']
    threads: fmt_threads
    shell: "lastdb -c -v -P {threads} {params.prot} {input} {params.db}"

rule lastdb8:
    input: '{db_root}.fasta'
    output: '{db_root}' + formatter_config['last8']['base'] \
                  + "." + formatter_config['last8']['suff']
    params:
        prot="-p" if is_prot else "",
        db='{db_root}' + formatter_config['last']['base']
    threads: fmt_threads
    shell: "lastdb8 -c -v -P {threads} {params.prot} {input} {params.db}"

rule bwadb:
    input: '{db_root}.fasta'
    output: '{db_root}' + formatter_config['bwa']['base'] \
                  + "." + formatter_config['bwa']['suff']
    params:
        db='{db_root}' + formatter_config['bwa']['base']
    threads: fmt_threads
    shell: "bwa index -T {threads} {input} {params.db}"

rule diamondb:
    input: '{db_root}.fasta'
    output: '{db_root}' + formatter_config['diamond']['base'] \
                  + "." + formatter_config['diamond']['suff']
    threads: fmt_threads
    shell: "diamond makedb --in {input} --db {output}"

rule blastdb:
    input: '{db_root}.fasta'
    output: '{db_root}' + formatter_config['blast']['base'] \
                  + "." + formatter_config['blast']['suff']
    params:
        type="prot" if is_prot else "nucl",
        db='{db_root}' + formatter_config['blast']['base']
    threads: fmt_threads
    shell: "makeblastdb -in {input} -dbtype {params.type} -parse_seqids -db {params.db}"

rule count:
    input: '{db_root}.fasta'
    output: '{db_root}.fasta.stats'
    shell: 'prinsiq-lite.pl -fasta {input} -stats_len -stats_info \
             > {output}'

rule get_db:
    input: FTP.remote(db_url, keep_local=True)
    output: db_fasta + ".gz"
    shadow: "shallow"
    shell: "mv {input} {output}"

rule hash:
    input:
        db=db_fasta + ".gz",
        hash=FTP.remote(db_url + '.md5', keep_local=True)
    output:
        hash=db_fasta + ".gz.md5",
    params:
        # the md5 file contains a different file
    shell:
        """
        mv {input.hash} {output.hash}
	# our filename is different than the name in the md5 file
        cat {output.hash} | perl -pe 's/.gz$/_{timestamp}.fasta.gz/' > {output.hash}.fixed
        cd $(dirname {output.hash}) && md5sum --check $(basename {output.hash}.fixed)
        """

rule make_fasta:
    input:
        db=db_fasta + ".gz",
        hash=db_fasta + ".gz.md5"
    output: db_fasta
    params:
        prot="-p" if is_prot else "",
    shell: "gunzip -c {input.db} | tantan {params.prot} > {output}"
